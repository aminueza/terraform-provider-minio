---
page_title: "minio_s3_bucket Resource - terraform-provider-minio"
subcategory: ""
description: |-
  
---

# minio_s3_bucket (Resource)



## Example Usage

```terraform
# Bucket protected from accidental deletion (default behavior)
resource "minio_s3_bucket" "state_terraform_s3" {
  bucket = "state-terraform-s3"
  acl    = "public"
  # force_destroy defaults to false - bucket deletion will fail if not empty
}

# Bucket that can be destroyed even with objects inside
resource "minio_s3_bucket" "temporary_data" {
  bucket        = "temporary-data"
  acl           = "private"
  force_destroy = true
}

# Bucket name prefix (create-only)
resource "minio_s3_bucket" "customer" {
  bucket_prefix = "customer-"
  acl           = "private"
}

# Bucket name prefix for globally-unique buckets while keeping existing buckets via migration
resource "minio_s3_bucket" "globally_unique_bucket" {
  bucket_prefix = "globally-unique-bucket-"
  acl           = "private"
}

output "minio_id" {
  value = minio_s3_bucket.state_terraform_s3.id
}

output "minio_url" {
  value = minio_s3_bucket.state_terraform_s3.bucket_domain_name
}
```

```terraform
resource "minio_s3_bucket" "data_lake" {
  bucket = "my-data-lake"

  tags = {
    Environment = "production"
    Team        = "data-engineering"
    CostCenter  = "12345"
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `acl` (String) Bucket's Access Control List (default: private)
- `bucket` (String) Name of the bucket
- `bucket_prefix` (String) Prefix of the bucket. Only used during bucket creation; ignored for existing resources.
- `force_destroy` (Boolean) A boolean that indicates all objects (including locked objects) should be deleted from the bucket so that the bucket can be destroyed without error. These objects are not recoverable.
- `object_locking` (Boolean) Enable object locking for the bucket (default: false)
- `quota` (Number) Quota of the bucket
- `tags` (Map of String) A map of tags to assign to the bucket

### Read-Only

- `arn` (String) ARN of the bucket
- `bucket_domain_name` (String) The bucket domain name
- `id` (String) The ID of this resource.

## Notes

- `bucket_prefix` is **create-only**. After a bucket exists in state, changes to `bucket_prefix` are ignored to avoid bucket replacement.
- During a `bucket` -> `bucket_prefix` migration, `bucket_prefix` may not be persisted in state (it can show up as empty in `terraform state show`) because diffs are suppressed to preserve the existing bucket.
- A migration from `bucket` to `bucket_prefix` can be done without replacement for an existing bucket when the current bucket name is compatible with the new prefix. For example:

```terraform
# Existing state
resource "minio_s3_bucket" "customer" {
  bucket = var.customer_name
}

# Migration (keeps existing bucket, new buckets use a generated suffix)
resource "minio_s3_bucket" "customer" {
  bucket_prefix = "${var.customer_name}-"
}
```

- Bucket tagging requires support from the underlying S3-compatible endpoint. If your endpoint does not support tagging, you can set `skip_bucket_tagging = true` in the provider configuration to disable tagging operations and prevent errors.

## Argument Reference

- `tags` - (Optional) A map of tags to assign to the bucket. Tags are key-value pairs that help you organize and categorize your buckets.

## Import

Import is supported using the following syntax:

```shell
# S3 buckets can be imported using the bucket name
terraform import minio_s3_bucket.example bucket-name
```
